plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.1'
    id 'io.gitlab.arturbosch.detekt' version '1.19.0'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.skyyo.template"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"https://release.com/\""
        }
        debug {
            minifyEnabled false
            buildConfigField "String", "BASE_URL", "\"https://debug.com/\""
        }
    }

    compileOptions.coreLibraryDesugaringEnabled = true
    kotlinOptions {
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
        useIR = true
    }
    buildFeatures.compose true

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

detekt {
    config = files("$rootDir/.detekt/config.yml")
}

ktlint {
    verbose = true
    android = true
    outputToConsole = true
    outputColorName = "RED"
    ignoreFailures = false
    enableExperimentalRules = false
    disabledRules = ["no-wildcard-imports", "max-line-length", "import-ordering"]
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    implementation "androidx.navigation:navigation-runtime-ktx:2.4.0-rc01"
    implementation "androidx.navigation:navigation-compose:2.4.0-rc01"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha02'

    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation 'com.google.android.material:material:1.4.0'

    implementation "androidx.compose.ui:ui:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation 'androidx.activity:activity-compose:1.4.0'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation("com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2")
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-rc01'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-material:$accompanist_version"
//    implementation "com.google.accompanist:accompanist-pager:0.16.1"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    implementation "com.google.accompanist:accompanist-insets:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"

    debugImplementation "com.github.YarikSOffice.Venom:venom:0.5.0"
    releaseImplementation "com.github.YarikSOffice.Venom:venom-no-op:0.5.0"

    testImplementation 'junit:junit:4.13.2'

}